//-----------------MOD SOURCE----------------//
//Modulation source page.
//--------------PARAMETERS:
//  1 - Mode : Modulation mode (0-free, 1-sync, 2-reset, 3-envelope)
/*BUG : currently only free  and reset modes work*/
//  2 - Polarity : The LFO's polarity (0-Unipolar[0-1], 1-Bipolar[-1,1])
//  3 - Shape : Mod carrier's waveform (0-sine, 1-tri, 2-saw, 3-square, 4-random)
//  4 - Rate : Mod carrier's rate
//  5 - Depth : Mod carrier's amplitude
//  6 - destVoice : The voice of destination
//  7 - destParam : The parameter of destination inside the chosen voice

import("stdfaust.lib");
import("wavetables.lib");

modsource(rate, depth, shape,
          mode, pol,
          destVoice, destParam, t) = (source : polarizer) * depth,
                                      destVoice, destParam
with {
    sine(trig) = hssine(rate, trig);
    tri(trig) = hstriangle(rate, trig);
    square(trig) = hssquare(rate, trig);
    saw(trig) = hssaw(rate, trig);
    rand = no.lfnoise(rate*10);
    carrier(reset) = (sine(reset), tri(reset), saw(reset), square(reset), rand)
                      : ba.selectn(5, shape);
    free = carrier(0);
    sync = carrier(0);
    reset = carrier(t);
    source = free,sync,reset : select3(mode);
    polarizer(sig) = (sig * 0.5 + 0.5, sig) : select2(pol);
};
